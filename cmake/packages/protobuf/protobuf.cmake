include(ExternalProject)

set(PROTO_EXT_PREFIX ${EXTERNAL_PROJECT_BINARY_DIR}/protobuf CACHE INTERNAL "")
set(PROTO_INSTALL_PREFIX ${PROTO_EXT_PREFIX}/install CACHE INTERNAL "")
set(PROTO_CONFIG_CMAKE_FILE "${PROTO_INSTALL_PREFIX}/lib/cmake/protobuf/protobuf-config.cmake" CACHE INTERNAL "")


set(PROTO_SRC_DIR ${PROTO_EXT_PREFIX}/src/protobuf_external CACHE INTERNAL "")
set(PROTO_INCLUDE_DIR ${PROTO_INSTALL_PREFIX}/include CACHE INTERNAL "")
set(PROTO_LIB_DIR ${PROTO_INSTALL_PREFIX}/lib CACHE INTERNAL "")
set(PROTO_LITE_LIBRARY ${PROTO_INSTALL_PREFIX}/lib/libprotobuf-lite.a CACHE INTERNAL "")
set(PROTO_BIN_DIR ${PROTO_INSTALL_PREFIX}/bin CACHE INTERNAL "")

set(PROTO_PROTOC_EXECUTABLE ${PROTO_BIN_DIR}/protoc CACHE INTERNAL "")
set(protobuf_generate_PROTOC_EXE ${PROTO_BIN_DIR}/protoc CACHE INTERNAL "")


set(PROTO_FILES
  ${PROJECT_ROOT}/runtime/proto/engine.proto
  ${PROJECT_ROOT}/runtime/proto/llm_metadata.proto
  ${PROJECT_ROOT}/runtime/proto/llm_model_type.proto
  ${PROJECT_ROOT}/runtime/proto/sampler_params.proto
  ${PROJECT_ROOT}/runtime/proto/token.proto
  ${PROJECT_ROOT}/runtime/executor/proto/constrained_decoding_options.proto
  ${PROJECT_ROOT}/runtime/util/external_file.proto
CACHE INTERNAL "")



setup_external_install_structure("${PROTO_INSTALL_PREFIX}")

if(NOT EXISTS "${PROTO_CONFIG_CMAKE_FILE}")
  message(STATUS "Protobuf not found. Configuring external build...")
  ExternalProject_Add(
    protobuf_external
    DEPENDS 
      absl_external
      gtest_external
    GIT_REPOSITORY
      https://github.com/protocolbuffers/protobuf
    GIT_TAG
      v6.31.1
    PREFIX
      ${PROTO_EXT_PREFIX}
    PATCH_COMMAND
        git checkout -- . && git clean -df
      COMMAND ${CMAKE_COMMAND} 
      -DPROTO_SRC_DIR=${PROTO_SRC_DIR}
      -DLITERTLM_PROTO_SHIM_PATH="${PROTOBUF_PACKAGE_DIR}/protobuf_shims.cmake"
      -P "${PROTOBUF_PACKAGE_DIR}/protobuf_patcher.cmake"
    CMAKE_ARGS
      -DCMAKE_PREFIX_PATH=${GTEST_INSTALL_PREFIX};${ABSL_INSTALL_PREFIX}
      -DCMAKE_INSTALL_PREFIX=${PROTO_INSTALL_PREFIX}
      -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
      -DCMAKE_POLICY_DEFAULT_CMP0169=OLD
      -DCMAKE_CXX_STANDARD=${CMAKE_CXX_STANDARD}
      "-DCMAKE_CXX_FLAGS=${CMAKE_CXX_FLAGS} -I${ABSL_INCLUDE_DIR}"
      -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
      -DCMAKE_POSITION_INDEPENDENT_CODE=ON
      -Dprotobuf_BUILD_TESTS=OFF
      -Dprotobuf_BUILD_LIBPROTOC=ON
      -Dprotobuf_BUILD_PROTOBUF_BINARIES=ON
      -Dprotobuf_LOCAL_DEPENDENCIES_ONLY=ON
      -Dabsl_DIR=${ABSL_INSTALL_PREFIX}/lib/cmake/absl
      -DGTest_DIR=${GTEST_INSTALL_PREFIX}/lib/cmake/GTest
      -DProtobuf_DIR=${PROTO_INSTALL_PREFIX}/lib/cmake/Protobuf
      
      -DABSL_PACKAGE_DIR=${ABSL_PACKAGE_DIR}
      -DABSL_INCLUDE_DIR=${ABSL_INCLUDE_DIR}
      -DABSL_LIB_DIR=${ABSL_LIB_DIR}
      -DLITERTLM_MODULES_DIR=${LITERTLM_MODULES_DIR}
      -DLITERTLM_PROTO_SHIM_PATH="${PROTOBUF_PACKAGE_DIR}/protobuf_shims.cmake"

    STEP_TARGETS
      verify_install_step
  )


  verify_install(protobuf_external ${PROTO_CONFIG_CMAKE_FILE})

else()
  message(STATUS "Protobuf already installed at: ${PROTO_INSTALL_PREFIX}")
  if(NOT TARGET protobuf_external)
    add_custom_target(protobuf_external)
  endif()
endif()

include(${PROTOBUF_PACKAGE_DIR}/protobuf_aggregate.cmake)
generate_protobuf_aggregate()



add_litertlm_library(litertlm_generated_protobuf STATIC)
add_dependencies(litertlm_generated_protobuf protobuf_external)

target_include_directories(litertlm_generated_protobuf
  PUBLIC
    ${CMAKE_BINARY_DIR}
    ${PROJECT_ROOT}
    ${PROTO_SRC_DIR}
    ${PROTO_INCLUDE_DIR}
    ${ABSL_INCLUDE_DIR}
)

target_link_libraries(litertlm_generated_protobuf
  PUBLIC
    protobuf::libprotobuf
    LiteRTLM::absl::absl
)

if(NOT TARGET protobuf::protoc)
    add_executable(protobuf::protoc IMPORTED GLOBAL)
    set_target_properties(protobuf::protoc PROPERTIES
        IMPORTED_LOCATION "${PROTO_PROTOC_EXECUTABLE}"
    )
endif()

generate_protobuf(litertlm_generated_protobuf)